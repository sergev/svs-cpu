*
* Test for instructions in stack mode:
*       ATX (15)
*       XTA (15)
*       WTC (15)
*       AAX (15)
*       ACX (15)
*       AEX (15)
*       ANX (15)
*       AOX (15)
*       APX (15)
*       ARX (15)
*       ASX (15)
*       AUX (15)
*
start   start   '1'
*
* Test for ATX in stack mode:
*
        vtm     ws(10)
        ita     10
        aex     cful
        ati     10
*
        xta
        atx     ws
        atx     ws+1
        atx     ws+2
        vtm     ws+1(15)
        xta     cful
        atx     (15)
        j+m     15(10)
        utm     1-2(15)         , -2-ws
        vjm     check(13)
*
        vtm     ws-1(15)
        xta
        atx     1(15)
        atx     3(15)
        xta     cful
        utc     2
        atx     (15)
        j+m     15(10)
        utm     2(15)           , 1-ws
        vjm     check(13)
*
* Test for XTA in stack mode:
*
        xta
        atx     ws+1
        vtm     ws+3(15)
        xta     (15)
        ati     2
        asn     64+24
        ati     3
        xta     (15)
        ati     4
        asn     64+24
        ati     5
        xta     (15)
        ati     6
        asn     64+32
        ati     7
        vjm     chst2e(13)
*
        vtm     ws+3(15)
        utc     -1
        xta     (15)
        ati     6
        asn     64+32
        ati     7
        xta     -2(15)
        ati     4
        asn     64+32
        ati     5
        xta     -3(15)
        ati     2
        asn     64+32
        ati     3
        utm     -3(15)
        vjm     chst2e(13)
*
* Test for WTC in stack mode:
*
        vtm     1(4)
        vtm     -1(7)
        vtm     -1(3)
        vtm     ws+3(15)
        wtc     (15)
        vtm     (6)
        wtc     (15)
        vtm     (4)
        wtc     (15)
        vtm     (2)
        vjm     chst2e(13)
*
        vtm     ws(15)
        xta     i1
        xts
        xts     i2
        xts     i3
        wtc     -2(15)
        vtm     (2)
        v1m     fail(2)
        j+m     15(10)
        utm     1-3(15)         , -3-ws
        v1m     fail(15)
*
* ALU instructions in stack mode:
* AAX, ACX, AEX, ANX, AOX, APX, ARX, ASX, AUX
*
        vtm     ws(15)
        xta     chess
        xts     chess+1
        aax     (15)
        u1a     fail
        xta     chess
        xts     chess+1
        arx     (15)
        xts     chess
        xts     chess+1
        aox     (15)
        aex     (15)
        u1a     fail
        xta     chess
        xts     chess+1
        xts     cful
        apx     (15)
        aux     (15)
        aex     chess
        u1a     fail
        xts     cful
        acx     (15)
        aex     i48
        u1a     fail
        xts     cful
        anx     (15)
        aex     i1
        u1a     fail
        xta     cful
        atx     (15)
        asx     (15)
        u1a     fail
pass    стоп    '12345'(6)
fail    стоп    '76543'(2)
*------------------------
check   v1m     fail(15)
        xta     ws
        ati     2
        v1m     fail(2)
        asn     64+24
        ati     2
        v1m     fail(2)
        xta     ws+2
        ati     2
        v1m     fail(2)
        asn     64+24
        ati     2
        v1m     fail(2)
        xta     ws+1
        ati     2
        asn     64+24
        ati     3
        utm     1(2)
        v1m     fail(2)
        utm     1(3)
        v1m     fail(3)
        xta     ufsl1
        atx     ws
        atx     ws+1
        atx     ws+2
        uj      (13)
chst2e  j+m     15(10)
        utm     1(15)           , -ws
        v1m     fail(15)
        utm     -1(2)
        v1m     fail(2)
        utm     1(3)
        v1m     fail(3)
        v1m     fail(4)
        v1m     fail(5)
        utm     -1(6)
        v1m     fail(6)
        utm     1(7)
        v1m     fail(7)
        uj      (13)
*-------------------------
align   адрес   start+'1777'            , данные с адреса 2000
cful    конд    b'7777777777777777'
chess   конд    b'5252525252525252'
        конд    b'2525252525252525'
i1      конд    b'1'
i2      конд    b'2'
i3      конд    b'3'
i48     конд    b'60'
ufsl1   конд    b'7777777700000001'
ws      пам     1030
        fin
